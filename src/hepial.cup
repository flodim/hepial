/*
Project: hepial compiler
Description: This is a compiler for hepial a custom language
			This si the cup file that contain the grammar rules.
Author: Florian Hassler & Dimitri Lizzi
Date: 14.01.2018
 */
import java_cup.runtime.*;
import java.io.*;
import symbole_table.*;
import abstract_tree.*;
import java.util.*;

parser code {:
private Stack<AbstractTree> treeStack;

private void enterBlock() {
    SymbTable.getInstance().enterBlock();
}

private void exitBlock() {
    SymbTable.getInstance().exitBlock();
}

private void addSymbol(Entry entry, Symb symbol){
    SymbTable.getInstance().add(entry,symbol);
}

public Stack<AbstractTree> getTreeStack() {
    return treeStack;
}

 :};

init with {:

treeStack=new Stack<AbstractTree>();

:};

terminal PRG, STARTPRG, ENDPRG, STARTFUNC, ENDFUNC, CONSTANT, COMMENT;
terminal SEMICOLON, OPENPARENT, CLOSEPARENT, EQUAL, COMMA, DOUBLEPOINTS;
terminal OPENBRACK, CLOSEBRACK, PLUS, MINUS, TILDA, TIMES, NOT, AND, OR, DIVIDE;
terminal EQUALS, DIFF, INF, SUP, INFEQUAL, SUPEQUAL, TRUESYM, FALSESYM;
terminal WHILESYM, DOSYM, ENDWHILE, IFSYM, THEN, ELSESYM, ENDIF;
terminal FORSYM, FROM, TO, ENDFOR, READ, WRITE, RETURNSYM;

terminal Integer INTEGERCONST;
terminal String STRINGCONST;
terminal String IDENT;
terminal String TINTEGER;
terminal String TBOOLEAN;

non terminal axiome, programme, entete, decla, declavar, declaconst ,decla_star, declafonct;
non terminal dim, expr, body, instr, affect;
non terminal write_instr, read_instr, cond, while_instr, for_instr, return_instr;
non terminal funccall, opebin, opeun, access, operand;
non terminal Integer index;
non terminal Integer funcparams;
non terminal ArrayList<Idf> lident;
non terminal String head;
non terminal Type typebase,type;
non terminal String array;
non terminal HashMap<Entry,Symb> param;

precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left EQUALS, DIFF;
precedence left INF, SUP, INFEQUAL, SUPEQUAL, AND, OR, DOUBLEPOINTS;
precedence right TILDA, NOT;

//HEPIAL GRAMMAR RULES (ordered like the exemple in the pdf)


axiome::= programme;

programme::=  entete decla_star STARTPRG {: enterBlock(); :} body {: exitBlock(); :} ENDPRG;

entete::= PRG IDENT;

decla_star::= decla decla_star |  ;

decla::= declavar | declaconst | declafonct;

declavar::= type:t lident:id_list SEMICOLON

{:
     for(int i=0; i<id_list.size(); i++){
          addSymbol(new Entry(id_list.get(i)), new Symb(t,false));
     }

:}
;

lident::=
    IDENT:id {:
    ArrayList<Idf>  id_list = new ArrayList<>();
    id_list.add(new Idf(id));
    RESULT= id_list;

    :}
    | lident:id_list COMMA IDENT:id {:

    id_list.add(new Idf(id));
    RESULT= id_list;

    :};

declaconst::= CONSTANT type:t IDENT:id EQUAL expr SEMICOLON
{:
    addSymbol(new Entry(new Idf(id)),new Symb(t,true));
:}
;

declafonct::=
    type IDENT:id OPENPARENT param:list_param
     {:
        for (HashMap.Entry<Entry,Symb> entry : list_param.entrySet()) {
                addSymbol(entry.getKey(),entry.getValue()); }
     :} CLOSEPARENT decla_star STARTFUNC {: enterBlock(); :} body {: exitBlock(); :} ENDFUNC

    |type IDENT:id OPENPARENT CLOSEPARENT decla_star STARTFUNC {: enterBlock(); :} body {: exitBlock(); :} ENDFUNC;

type::= typebase:tb {: RESULT=tb;:} | array:a ;

typebase::=
    TINTEGER {: RESULT= TypeInteger.getInstance(); :} |
    TBOOLEAN {: RESULT= TypeBoolean.getInstance(); :};

array::= typebase OPENBRACK dim CLOSEBRACK;

dim::=
    expr DOUBLEPOINTS expr
    |dim COMMA expr DOUBLEPOINTS expr;

param::=
    type:t IDENT:id
    {:
        HashMap<Entry,Symb> list_param= new HashMap<>();
        list_param.put(new Entry(new Idf(id)),new Symb(t,false));
        RESULT=list_param;

    :}
    |param:list_param COMMA type:t IDENT:id
    {:
        list_param.put(new Entry(new Idf(id)),new Symb(t,false));
        RESULT= list_param;
    :};


body ::= instr body
{:
    Block b = (Block) treeStack.pop();
    Instruction i = (Instruction) treeStack.pop();
    b.addInstructionBefore(i);
    treeStack.push(b);
:}
 | {: treeStack.push(new Block()); :};



instr::= affect | write_instr | read_instr | cond  | return_instr | while_instr | for_instr;

read_instr::= READ IDENT:id SEMICOLON
{:
    treeStack.push(new ReadInstr(new Idf(id)));
:};

write_instr::=
    WRITE expr SEMICOLON
    {:
        Expression e = (Expression) treeStack.pop();
        treeStack.push(new WriteInstr(e));
    :};

affect::= access EQUAL expr SEMICOLON
{:
    Expression source = (Expression) treeStack.pop();
    Idf dest= (Idf)treeStack.pop();
    treeStack.push(new Affectation(source,dest));
:};

return_instr::= RETURNSYM expr SEMICOLON
{:
   Expression expr = (Expression) treeStack.pop();
   treeStack.push(new ReturnInstr(expr));
 :};

cond::= IFSYM expr  THEN {: enterBlock(); :} body {: exitBlock(); :} ELSESYM {: enterBlock(); :} body {: exitBlock(); :} ENDIF
{:
    Block elseBlock= (Block)treeStack.pop();
    Block thenBlock= (Block)treeStack.pop();
    Expression cond= (Expression)treeStack.pop();
    treeStack.push(new Condition(cond,thenBlock,elseBlock));
:};

while_instr::= WHILESYM expr DOSYM {: enterBlock(); :} body {: exitBlock(); :} ENDWHILE
{:
    Block instructions= (Block)treeStack.pop();
    Expression cond= (Expression)treeStack.pop();
    treeStack.push(new WhileLoop(cond,instructions));
:};

for_instr::= FORSYM IDENT:var FROM expr TO expr DOSYM {: enterBlock(); :} body {: exitBlock(); :} ENDFOR
{:
    Block instructions =(Block)treeStack.pop();
    Expression upperBoundary = (Expression) treeStack.pop();
    Expression lowerBoundary = (Expression) treeStack.pop();
    treeStack.push(new ForLoop(new Idf(var),lowerBoundary,upperBoundary,instructions));
:};

expr::=
    opebin
    {:
        Binary oper = (Binary) treeStack.pop();
        Expression right =(Expression)treeStack.pop();
        Expression left = (Expression)treeStack.pop();
        oper.setLeft(left);
        oper.setRight(right);
        treeStack.push(oper);
    :}
    |opeun
    {:
        Unary oper = (Unary)treeStack.pop();
        Expression exp= (Expression)treeStack.pop();
        oper.setExp(exp);
        treeStack.push(oper);
    :}
    |OPENPARENT expr CLOSEPARENT
    |operand;

access::= IDENT:name {:
    treeStack.push(new Idf(name));
:}| IDENT index PLUS;

index::= OPENBRACK expr CLOSEBRACK;

operand::= access | funccall
| INTEGERCONST:i {: treeStack.push(new IntNumber(i)); :}
| TRUESYM {: treeStack.push(new BooleanValue(true));:}
| FALSESYM {: treeStack.push(new BooleanValue(false)); :}
| STRINGCONST:s {: treeStack.push(new StringValue(s)); :};

opebin::=
  expr PLUS expr  {: treeStack.push(new Addition()); :}
| expr MINUS expr {: treeStack.push(new Substraction()); :}
| expr TIMES expr {: treeStack.push(new Multiplication()); :}
| expr DIVIDE expr {: treeStack.push(new Division()); :}
| expr EQUALS expr {: treeStack.push(new Equal()); :}
| expr DIFF expr {: treeStack.push(new NotEqual()); :}
| expr INF expr {: treeStack.push(new Inferior()); :}
| expr SUP expr {: treeStack.push(new Superior()); :}
| expr INFEQUAL expr {: treeStack.push(new InfEqual()); :}
| expr SUPEQUAL expr {: treeStack.push(new SupEqual()); :}
| expr AND expr {: treeStack.push(new And()); :}
| expr OR expr {: treeStack.push(new Or()); :};

opeun::=
  TILDA expr {: treeStack.push(new Tilda()); :}
| NOT expr {: treeStack.push(new Not()); :};

funccall::=
    IDENT:id OPENPARENT CLOSEPARENT
    {:
        treeStack.push(new Call(new Idf(id)));
    :}
    | IDENT:id OPENPARENT funcparams:nb_param CLOSEPARENT
    {:
        ArrayList<Expression> params = new ArrayList<>();
        for(int i=0;i<nb_param;i++){
            Expression exp = (Expression)treeStack.pop();
            params.add(exp);
        }
        treeStack.push(new Call(new Idf(id),params));
    :}
    ;

funcparams::= expr
{:
    Integer cnt=1;
    RESULT=cnt;
:}
| funcparams:nb_param COMMA expr
    {:
        RESULT=nb_param++;
    :}
;

